\section{Selective Functors}\label{sec-selective}

In this section we introduce selective applicative functors, which we will
subsequently refer to as simply \emph{selective functors}, for brevity. We start
by defining the new abstraction, and then use it in~\S\ref{sec-combinators} to
implement several derived combinators, such as the aforementioned \hs{whenS}.
In~\S\ref{sec-instances} we provide several examples of selective functors, and
further discuss the relationships between applicative functors, selective
functors, and monads. In~\S\ref{sec-laws}, these relationships are further
elaborated and expressed as a set of laws that all selective functors are
required to satisfy.

Like applicative functors~\citep{mcbride2008applicative}, selective functors
provide a way to embed pure values into an effectful context \hs{f} using the
function \hs{pure}, and give meaning to composition of two \emph{independent}
effectful computations using the operator \hs{<*>}. See Fig.~\ref{fig-types} for
the standard definition of the corresponding type class \hs{Applicative}.
Selective functors enrich the applicative interface with the \hs{select} method,
which gives meaning to the composition of two effectful computations, where, in
contrast to \hs{<*>}, the second computation \emph{depends} on the first one:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
class Applicative f => Selective f where
    select :: f (Either a b) -> f (a -> b) -> f b
\end{minted}
\vspace{1mm}

\noindent
One can think of \hs{select} as a selective function application:
parametricity~\citep{wadler1989theorems} dictates that, when given a
\hs{Left}~\hs{a}, we \emph{must execute the effects} in
\hs{f}~\hs{(}\hs{a}~\hs{->}~\hs{b)}, apply the obtained function to \hs{a}, and
return the resulting \hs{b}; on the other hand, when given a \hs{Right}~\hs{b},
we \emph{may skip the effects} associated with the function, and return the
given \hs{b}\footnote{Note, however, that if \hs{f}~\hs{a} holds no values of
type \hs{a}, i.e. \hs{a} is a \emph{phantom type
variable}~\cite{leijen2000domain}, then the effects in
\hs{f}~\hs{(}\hs{a}~\hs{->}~\hs{b)} can be skipped unconditionally. The
selective functor \hs{Under} is a good example (see~\S\ref{sec-instances}).}.

Following the notational convention for applicative operators, we also define
the  left-associative infix operator alias \hs{<*?} for \hs{select}: the angle
bracket pointing to the left means we always use the corresponding value; the
value on the right, however, may be skipped, hence the question mark.

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
class Functor f where
    fmap :: (a -> b) -> f a -> f b
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- An infix left-associative synonym for fmap
(<$>) :: Functor f => (a -> b) -> f a -> f b      -- (<$>) is pronounced "fmap" or "map"
\end{minted}
\vspace{2mm}
\hrule
\vspace{2mm}
\begin{minted}[fontsize=\small]{haskell}
class Functor f => Applicative f where
    pure  :: a -> f a
    (<*>) :: f (a -> b) -> f a -> f b             -- (<*>) is pronounced "apply"
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- A variant of (<*>) that discards the value of the first argument
(*>) :: Applicative f => f a -> f b -> f b
\end{minted}
\vspace{2mm}
\hrule
\vspace{2mm}
\begin{minted}[fontsize=\small]{haskell}
class Applicative f => Selective f where
    select :: f (Either a b) -> f (a -> b) -> f b
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- An infix left-associative synonym for select
(<*?) :: f (Either a b) -> f (a -> b) -> f b      -- (<*?) is pronounced "select"
\end{minted}
\vspace{2mm}
\hrule
\vspace{2mm}
\begin{minted}[fontsize=\small]{haskell}
class Selective f => Monad f where
    return :: a -> f a                            -- Note the law: return = pure
    (>>=)  :: f a -> (a -> f b) -> f b            -- (>>=) is pronounced "bind"
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- A monadic equivalent of the apply operator, satisfying the law (<*>) = ap
@\blk{ap}@ :: Monad f => f (a -> b) -> f a -> f b
\end{minted}
% \vspace{-2mm}
\caption{The proposed type class hierarchy, where \hs{Functor}, \hs{Applicative}
and \hs{Monad} are standard Haskell type classes, and \hs{Selective} is
a new intermediate abstraction introduced between \hs{Applicative} and
\hs{Monad}.}\label{fig-types}
% \vspace{-2mm}
\end{figure}

One can implement \hs{select} using monads in a straightforward manner: examine
the value produced by \hs{f}~\hs{(Either}~\hs{a}~\hs{b)} with the bind operator,
and then, in the \hs{Left}~\hs{a} case, execute the subsequent effect
\hs{f}~\hs{(@@a}~\hs{->}~\hs{b)}, passing the \hs{a} to it using the
\hs{Functor}'s map operator, as shown below. We will use~\hs{<@\dollar@>}, an
infix synonym of \hs{fmap}, throughout the paper (see Fig.~\ref{fig-types} for
\hs{Functor}'s API).

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
selectM :: Monad f => f (Either a b) -> f (a -> b) -> f b
selectM x y = x >>= \e -> case e of Left  a -> ($a) <$> y -- Execute y
                                    Right b -> pure b     -- Skip y
\end{minted}
\vspace{1mm}

\noindent
Many monads directly use \hs{select}~\hs{=}~\hs{selectM} in their \hs{Selective}
instance definitions, and in \S\ref{sec-laws} we argue that this should in fact
be a law when both \hs{Selective}~\hs{f} and \hs{Monad}~\hs{f} instances exist.
Note that some monads, e.g. the \Haxl monad (\S\ref{sec-haxl}), choose to
implement the  \hs{select} method differently for performance reasons, but they
still satisfy the law \hs{select}~\hs{=}~\hs{selectM} at the semantic level.

One can also implement a function with the type signature of \hs{select} using
applicative functors, but it will always execute the effects associated with the
second argument, rendering any conditional execution of effects impossible, as
in the \hs{pingPongA} example in the introduction~(\S\ref{sec-intro}):

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
selectA :: Applicative f => f (Either a b) -> f (a -> b) -> f b
selectA x y = (\e f -> either f id e) <$> x <*> y -- Execute x and y
\end{minted}
\vspace{1mm}

\noindent
Fig.~\ref{fig-std} gives type signatures and short descriptions for standard
functions \hs{either}, \hs{id}, and other convenient functional combinators that
we use in this paper.

While \hs{selectM} is useful for \emph{conditional execution} of effects,
\hs{selectA} is useful for \emph{static analysis}. As we will see
in~\S\ref{sec-instances}, selective functors used for static analysis need to
collect information about all possible effects instead of skipping some of them,
hence they directly use \hs{select}~\hs{=}~\hs{selectA} in their \hs{Selective}
instance definitions.

\begin{table*}[t]
\caption{Comparison of apply, select and bind operators in terms of their
expressive power. We discuss static analysis in~\S\ref{sec-instances}
and~\S\ref{sec-static}; parallelism and speculative execution
in~\S\ref{sec-haxl}; conditional and arbitrary dynamic effects
in~\S\ref{sec-combinators}~(\hs{whenS}) and~\S\ref{sec-free-ping-pong}
(\hs{greeting}). Note that each operator has one unique ability that the two
others lack.
\label{tab-operators}}
\vspace{-2mm}
\smaller
\centering
\begin{tabular}{l||c|c|c}
Notions that can be expressed using an operator & apply (\hs{<*>}) & select (\hs{<*?}) & bind (\hs{>>=}) \\\hline
Arbitrary dynamic effects                       & ~                & ~                 & \checkmark      \\
Conditional execution of effects                & ~                & \checkmark        & \checkmark      \\
Speculative execution of effects                & ~                & \checkmark        & ~               \\
Static visibility and analysis of effects       & \checkmark       & \checkmark        & ~               \\
Independent effects and parallelism             & \checkmark       & ~                 & ~               \\
\end{tabular}
\end{table*}

Any \hs{Applicative} instance can thus be given a \hs{Selective} instance. The
opposite is also true in the sense that one can recover the operator \hs{<*>}
from \hs{select} as follows:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
apS :: Selective f => f (a -> b) -> f a -> f b
apS f x = select (Left <$> f) ((&) <$> x)
\end{minted}
\vspace{1mm}

\noindent
Here we tag the function \hs{a}~\hs{->}~\hs{b} with \hs{Left} and apply the
reverse function application~(see Fig.~\ref{fig-std}) to~the~value \hs{a}, thus
matching the type signature of \hs{select}. Since the \hs{Right} case is
impossible, the effect \hs{x}~\hs{::}~\hs{f}~\hs{a} is executed unconditionally.
Note, however, that in general \hs{<*>} and \hs{apS} are not equivalent.
Selective functors that satisfy the property \hs{(<*>)}~\hs{=}~\hs{apS} will be
called \emph{rigid}; they will turn out to have a simple normal form, which we
will exploit in the free construction in~\S\ref{sec-free}.

It is worth emphasising that the subclass relationships
\hs{Applicative}~\hs{f}~\hs{=>}~\hs{Selective}~\hs{f} and
\hs{Applicative}~\hs{f}~\hs{=>}~\hs{Monad}~\hs{f} are different. \emph{Some
applicative functors are not monads}, e.g. the \hs{Const} functor
(\S\ref{sec-instances}), but \emph{every applicative functor is also a selective
functor}, as witnessed by the function \hs{selectA}. The subclass relationship
\hs{Applicative}~\hs{f}~\hs{=>}~\hs{Selective}~\hs{f} is justified only by the
extra method \hs{select} in \hs{Selective}. While
\hs{select}~\hs{=}~\hs{selectA} is a valid implementation of \hs{select},
\emph{it is not the only useful implementation}, as will be demonstrated
in~\S\ref{sec-instances}. The applicative-selective-monad hierarchy therefore
reflects method set inclusion:
$\{\hs{<*>}\} \subset \{\hs{<*>}, \hs{select}\} \subset \{\hs{<*>}, \hs{select}, \hs{>>=}\}$.
Table~\ref{tab-operators} compares the three methods in terms their expressive
power. Different applications require different sets of methods; for example, as
we will see in~\S\ref{sec-haxl}, \Haxl requires all three: \hs{<*>} for
parallelism, \hs{select} for speculative execution, and \hs{>>=} for arbitrary
dynamic effects.

We will come back to the relationship between applicative functors, selective
functors and monads in \S\ref{sec-laws}, after first exploring \emph{selective
combinators} that can be written using the selective interface
(\S\ref{sec-combinators}), and then looking at some concrete examples of
selective functors (\S\ref{sec-instances}).

\begin{figure}[h]
\begin{minted}[fontsize=\small]{haskell}
($)     :: (a -> b) -> a -> b                               -- Function application
(&)     :: a -> (a -> b) -> b                               -- Reverse function application
(.)     :: (b -> c) -> (a -> b) -> a -> c                   -- Function composition
id      :: a -> a                                           -- Identity function
const   :: a -> b -> a                                      -- Constant function
flip    :: (a -> b -> c) -> b -> a -> c                     -- Flip function arguments
uncurry :: (a -> b -> c) -> (a, b) -> c                     -- Uncurry a function
foldr   :: (a -> b -> b) -> b -> [a] -> b                   -- Reduce a list to a value
bool    :: a -> a -> Bool -> a                              -- Deconstruct a Bool
maybe   :: b -> (a -> b) -> Maybe a -> b                    -- Deconstruct a Maybe
either  :: (a -> c) -> (b -> c) -> Either a b -> c          -- Deconstruct an Either
first   :: (a -> c) -> Either a b -> Either c b             -- Map over Left
bimap   :: (a -> c) -> (b -> d) -> Either a b -> Either c d -- Map over Left and Right
void    :: Functor f => f a -> f ()                         -- Discard an effect's value
\end{minted}
\vspace{-2mm}
\caption{Type signatures and descriptions of standard operators and functions
used throughout the paper.}\label{fig-std}
\vspace{-3mm}
\end{figure}

\subsection{Selective Combinators}\label{sec-combinators}

As a first use-case of the interface provided by selective functors, let us
revisit our ping-pong example from~\S\ref{sec-intro} and implement the
combinator \hs{whenS}:

\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
whenS :: Selective f => f Bool -> f () -> f ()
whenS x y = selector <*? effect
  where
    selector = bool (Right ()) (Left ()) <$> x -- NB: convert True to Left ()
    effect   = const                     <$> y
\end{minted}
\vspace{1mm}

\noindent
We first bring the given effectful computations into the right shape by using
the \hs{Functor}'s map operator. Specifically, \hs{x}~\hs{::}~\hs{f}~\hs{Bool}
is converted into the \hs{selector}~\hs{::}~\hs{f}~\hs{(Either}~\hs{()}~\hs{())},
and \hs{y}~\hs{::}~\hs{f}~\hs{()} is converted into the
\hs{effect}~\hs{::}~\hs{f}~\hs{(()}~\hs{->}~\hs{())}. The results are composed
using the select operator~\hs{<*?}, and the meaning of this composition is
determined by the supplied \hs{Selective}~\hs{f} instance. For example, an
instance like \hs{f}~\hs{=}~\hs{IO} would skip \hs{y} if \hs{x} yields
\hs{False}, as exploited by our implementation of \hs{pingPongS}. On the other
hand, instances used for static analysis would record both \hs{x} and \hs{y} as
possible effects. See more examples in~\S\ref{sec-instances}.

It is worth noting that unlike the select operator, whose implementation is
almost completely determined by parametricity (i.e., the only real question is:
\emph{``To skip, or not to skip?''}), \hs{whenS} admits a variety of (incorrect)
implementations. In particular, due to \emph{Boolean blindness}\footnote{The
term refers to the fact that the \hs{True} and \hs{False} values are not
distinguished at the type level, see~\citet{boolean-blindness}.},
it is easy to inadvertently implement \hs{unlessS}, which has the same type but
flips the meaning of the Boolean value. The ability to reason parametrically was
one of the guiding principles we used when looking for a good abstraction for
selective functors: \hs{select} provides this ability, whereas \hs{whenS} does
not.

% Constraints liberate, liberties constrain
% Dijkstra: "Being abstract is something profoundly different from being vague â€¦
% The purpose of abstraction is not to be vague, but to create a new semantic
% level in which one can be absolutely precise."

A strong contender for playing the leading role in selective functors is the
function \hs{branch} that, given an effectful computation
\hs{x}~\hs{::}~\hs{f}~\hs{(Either}~\hs{a}~\hs{b)}, selects which of the two
subsequent computations, namely
\hs{l}~\hs{::}~\hs{f}~\hs{(}\hs{a}~\hs{->}~\hs{c)} or
\hs{r}~\hs{::}~\hs{f}~\hs{(}\hs{b}~\hs{->}~\hs{c)}, to execute:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
branch :: Selective f => f (Either a b) -> f (a -> c) -> f (b -> c) -> f c
branch x l r = fmap (fmap Left) x <*? fmap (fmap Right) l <*? r
\end{minted}
\vspace{1mm}

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
whenS :: Selective f => f Bool -> f () -> f ()
whenS x y = select (bool (Right ()) (Left ()) <$> x) (const <$> y)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
branch :: Selective f => f (Either a b) -> f (a -> c) -> f (b -> c) -> f c
branch x l r = fmap (fmap Left) x <*? fmap (fmap Right) l <*? r
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
ifS :: Selective f => f Bool -> f a -> f a -> f a
ifS x t e = branch (bool (Right ()) (Left ()) <$> x) (const <$> t) (const <$> e)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
(<||>) :: Selective f => f Bool -> f Bool -> f Bool
x <||> y = ifS x (pure True) y
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
(<&&>) :: Selective f => f Bool -> f Bool -> f Bool
x <&&> y = ifS x y (pure False)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
fromMaybeS :: Selective f => f a -> f (Maybe a) -> f a
fromMaybeS x mx = select (maybe (Left ()) Right <$> mx) (const <$> x)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
anyS :: Selective f => (a -> f Bool) -> [a] -> f Bool
anyS p = foldr ((<||>) . p) (pure False)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
allS :: Selective f => (a -> f Bool) -> [a] -> f Bool
allS p = foldr ((<&&>) . p) (pure True)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
whileS :: Selective f => f Bool -> f () -- Run a computation while it yields True
whileS x = whenS x (whileS x)
\end{minted}
\vspace{-2mm}
\caption{A library of selective combinators. The names and order of parameters
are inherited from the standard Haskell library. For example, \hs{fromMaybeS}
corresponds to the standard
\hs{fromMaybe}~\hs{::}~\hs{a}~\hs{->}~\hs{Maybe}~\hs{a}~\hs{->}~\hs{a} and
retains the short-circuiting behaviour, i.e. if the second argument yields a
\hs{Just}, the first argument is skipped.}
\label{fig-library}
\vspace{-3mm}
\end{figure}

\noindent
While we encourage the reader to derive an implementation of \hs{branch} as an
exercise, we would like to share our intuition behind it, as it will be useful
for \emph{free selective functors} in~\S\ref{sec-free}. The select operator
allows us to eliminate one of the cases in a sum type, namely the
\hs{Left}~\hs{a} case in \hs{Either}~\hs{a}~\hs{b}, leaving the other case
intact. To implement \hs{branch}, we will need to apply \hs{<*?} twice,
eliminating \hs{a} and \hs{b} one after another. The first application is tricky
because \hs{f}~\hs{(Either}~\hs{a}~\hs{b)} and
\hs{f}~\hs{(}\hs{a}~\hs{->}~\hs{c)} do not match the type signature of \hs{<*?}.
To fix the mismatch, we convert them to
\hs{f}~\hs{(Either}~\hs{a}~\hs{(}\hs{Either}~\hs{b}~\hs{c))} and
\hs{f}~\hs{(}\hs{a}~\hs{->}~\hs{Either}~\hs{b}~\hs{c)}, respectively. The second
application of \hs{<*?} is then straightforward.

As will be discussed in~\S\ref{sec-alt-multi}, we could have chosen to use
\hs{branch} instead of \hs{select} as the method of the \hs{Selective} type
class. Our choice of \hs{select} follows the Occam's razor principle:
\hs{select} is simpler than \hs{branch}, which, in particular, leads to a
simpler free construction (\S\ref{sec-free-construction}).

By instantiating \hs{select} with \hs{a}~\hs{=}~\hs{b}~\hs{=}~\hs{()} we have
earlier obtained \hs{whenS}. Below we repeat the exercise with \hs{branch},
obtaining another familiar conditional combinator \hs{ifS}:

\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
ifS :: Selective f => f Bool -> f a -> f a -> f a
ifS x t e = branch selector (const <$> t) (const <$> e)
  where
    selector = bool (Right ()) (Left ()) <$> x -- NB: convert True to Left ()
\end{minted}
\vspace{1mm}

\noindent
Many conditional combinators, which are typically associated with the \hs{Monad}
type class, can be expressed using selective functors, as shown in
Fig.~\ref{fig-library}, making them reusable in new contexts. In particular, the
logical combinators \hs{<||>} and \hs{<&&>} will play an important role in
improving the efficiency of the \Haxl framework in~\S\ref{sec-haxl}. To
emphasise the monadic flavour of selective functors, we can use \hs{ifS} to
implement the bind operator specialised to \hs{Bool}:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
bindBool :: Selective f => f Bool -> (Bool -> f a) -> f a
bindBool x f = ifS x (f False) (f True)
\end{minted}
\vspace{1mm}

\noindent
This can be achieved not only for \hs{Bool}, but for any \emph{enumerable}
type, as we will discuss in~\S\ref{sec-alt-multi}.

\subsection{Examples of Selective Functors}\label{sec-instances}

Having explored various useful combinators that can be implemented on top of the
minimalistic selective interface, in this section we look at several examples of
selective functors.

As we have observed at the beginning of this section, any monad can be given a
\hs{Selective} instance simply by using \hs{select}~\hs{=}~\hs{selectM} as the
definition. As an example, below we define a selective instance for \hs{IO}, and
test the function \hs{pingPongS} from \S\ref{sec-intro} in an interactive GHC
session:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Selective IO where select = selectM

@\ghci@ pingPongS = whenS ((=="ping") <$> getLine) (putStrLn "pong")
@\ghci@ pingPongS
hello
@\ghci@ pingPongS
ping
pong
\end{minted}
\vspace{1mm}

\noindent
As desired, the effect \hs{putStrLn}~\hs{"pong"} occurs conditionally.
In~\S\ref{sec-free-ping-pong} we will see how to statically analyse
\hs{pingPongS}, i.e. obtain a list of all possible effects \emph{before the
execution}, which can be done by interpreting \hs{pingPongS} in the selective
functor \hs{Over}, introduced below.

We first recall the functor \hs{Const}~\hs{m}~\hs{a}, which is an interesting
and surprisingly useful instance of the \hs{Applicative} type class. It stores
no values of type \hs{a}, but keeps track of the applicative computation
structure in the monoid value of type \hs{m}:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
newtype Const m a = Const { getConst :: m } -- Defined in Data.Functor.Const
\end{minted}
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Functor (Const m) where
    fmap _ (Const x) = Const x
\end{minted}
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
-- 'mempty' and '<>' are the identity and the binary operation of the Monoid m
instance Monoid m => Applicative (Const m) where
    pure _              = Const mempty   -- Pure values have no effects
    Const x <*> Const y = Const (x <> y) -- Collect effects in x and y
\end{minted}
\vspace{1mm}

\noindent
It turns out there are two useful selective instances for \hs{Const}. To
disambiguate between them, we will call them \hs{Over} and \hs{Under},
reusing\footnote{Fortunately, thanks to the new GHC extension
\cmd{DerivingVia}~\citep{blondal2018deriving}, we can reuse \hs{Const}
instances without duplicating any code, simply by adding
\hs{deriving}~\hs{(Functor,}~\hs{Applicative)}~\hs{via}~\hs{(Const}~\hs{m)} to
the \hs{newtype} definitions.}
the above \hs{Functor} and \hs{Applicative} instances:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
newtype Over  m a = Over  { getOver  :: m }
newtype Under m a = Under { getUnder :: m }

instance Monoid m => Selective (Over m) where
    select (Over x) (Over y) = Over (x <> y) -- Collect effects in x and y
\end{minted}
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Monoid m => Selective (Under m) where
    select (Under x) _ = Under x             -- Discard conditional effects
\end{minted}
\vspace{1mm}

\noindent
The selective functor \hs{Over} can be used for computing a list of all effects
embedded in a computation, i.e. an \emph{over-approximation} of the effects that
will actually occur. This is achieved by keeping track of effects in both
arguments of the select operator. The selective functor \hs{Under}, on the other
hand, discards the second argument of \hs{select}, and therefore computes an
\emph{under-approximation}, i.e. a list of effects that are guaranteed to occur.
Let us give these two instances a try:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
@\ghci@ ifS (Over@\,@"a") (Over@\,@"b") (Over@\,@"c") *> Over@\,@"d" *> whenS (Over@\,@"e") (Over@\,@"f")
Over "abcdef"
\end{minted}
\vspace{1mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
@\ghci@ ifS (Under@\,@"a") (Under@\,@"b") (Under@\,@"c") *> Under@\,@"d" *> whenS (Under@\,@"e") (Under@\,@"f")
Under "ade"
\end{minted}
\vspace{1mm}

\noindent
As expected, \hs{Over} collects all effects, whereas \hs{Under} does not look
beyond ``opaque'' conditions. A deeper difference between them is that \hs{Over}
is a rigid selective functor, i.e. \hs{(<*>)}~\hs{=}~\hs{apS}, but \hs{Under} is
not. Indeed, \hs{Under}~\hs{"a"}~\hs{<*>}~\hs{Under}~\hs{"b"} records both
\hs{"a"} and \hs{"b"}, but \hs{apS}~\hs{(Under}~\hs{"a")}~\hs{(Under}~\hs{"b")}
records just \hs{"a"} because \hs{apS} is implemented via \hs{select} and
therefore lacks information about the independence of the two effects.
Intuitively, non-rigid selective functors have a richer structure due to the
fact that \hs{<*>} is not expressible via \hs{select}.

% , which in particular complicates the
% corresponding free construction (\S\ref{sec-free-construction}).

Our last example in this section is the selective functor
\hs{Validation}\footnote{Applicative functors \hs{Const} and \hs{Validation}
appeared under the names \hs{Accy} and \hs{Except} in
\citep{mcbride2008applicative}.}, which is useful for validating complex data:
if reading one or more data fields has failed, all errors are accumulated (using
the operator \hs{<>} from the semigroup \hs{e}) to be reported together.

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
data Validation e a = Failure e | Success a deriving Functor
\end{minted}
\vspace{0mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Semigroup e => Applicative (Validation e) where
    pure = Success
    Failure e1 <*> Failure e2 = Failure (e1 <> e2) -- Accumulate errors
    Failure e1 <*> Success _  = Failure e1
    Success _  <*> Failure e2 = Failure e2
    Success f  <*> Success a  = Success (f a)
\end{minted}
\vspace{1mm}

\noindent
The error-accumulating behaviour cannot be extended to a \hs{Monad} instance:
after the first \hs{Failure} we have no value to pass to the second argument of
the bind operator. We can, however, define the following \hs{Selective}
instance, which allows us to validate data in the presence of conditions.

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Semigroup e => Selective (Validation e) where
    select (Success (Left  a)) f = ($a) <$> f
    select (Success (Right b)) _ = Success b -- Skip after false conditions
    select (Failure e)         _ = Failure e -- Skip after failed conditions
\end{minted}
\vspace{1mm}

\noindent
Similarly to \hs{Under}, we discard the second argument but only if the first
has resulted in a \hs{Right} value or failed. This allows us not to report
validation errors in inactive branches or in branches hidden behind failed
conditionals (the corresponding fields are not necessarily needed).

Below we define a function that constructs a shape (a circle or a rectangle)
given a choice of the shape \hs{x}, and the shape's parameters (\hs{r}, \hs{w},
and \hs{h}) in an arbitrary selective functor \hs{f}. You can think of the
inputs as results of reading the corresponding fields from a web form, where
\hs{x} is a checkbox, and all other fields are numeric textboxes, some of which
may be empty.

\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
data Shape = Circle Radius | Rectangle Width Height
\end{minted}
\vspace{0mm}
\begin{minted}[xleftmargin=10pt]{haskell}
shape :: Selective f => f Bool -> f Radius -> f Width -> f Height -> f Shape
shape x r w h = ifS x (Circle <$> r) (Rectangle <$> w <*> h)
\end{minted}
\vspace{0.5mm}

\noindent
We choose \hs{f}~\hs{=}~\hs{Validation}~\hs{[String]} to report the errors that
occurred when reading values from the form. Let us see how this works.

\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
@\ghci@ shape (Success True) (Success 1) (Failure ["width?"]) (Failure ["height?"])
Success (Circle 1)
@\ghci@ shape (Success False) (Failure ["radius?"]) (Success 2) (Success 3)
Success (Rectangle 2 3)
@\ghci@ shape (Success False) (Success 1) (Failure ["width?"]) (Failure ["height?"])
Failure ["width?", "height?"]
@\ghci@ shape (Failure ["choice?"]) (Failure ["radius?"]) (Success 2) (Failure ["height?"])
Failure ["choice?"]
\end{minted}
\vspace{0.5mm}

\noindent
In the last example, since the shape's choice could not be read, we do not
report any subsequent errors. But it does not mean we are short-circuiting the
validation: we will continue accumulating errors as soon as we get out of the
failed conditional, as demonstrated below.

\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
twoShapes :: Selective f => f Shape -> f Shape -> f (Shape, Shape)
twoShapes s1 s2 = (,) <$> s1 <*> s2
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt,fontsize=\small]{haskell}
@\ghci@ s1 = shape (Failure ["choice 1?"]) (Success 1) (Failure ["width 1?"]) (Success 3)
@\ghci@ s2 = shape (Success False) (Success 1) (Success 2) (Failure ["height 2?"])
@\ghci@ twoShapes s1 s2
Failure ["choice 1?","height 2?"]
\end{minted}
\vspace{0.5mm}

\noindent
Like \hs{Under}, the \hs{Validation} instance is not a rigid selective functor
because \hs{select} occasionally discards effects in the second argument.

\subsection{Laws}\label{sec-laws}

Now that we have seen several instances of selective functors, it is time to
discuss the laws that we expect these instances to satisfy. In particular, it is
very useful to know how the selective interface interacts with existing
applicative and monadic interfaces. Real code might mix all of these
abstractions, because each of them is useful in its own right, and the laws
presented in this section allow us to safely refactor such code while keeping
its original meaning.

Fig.~\ref{fig-laws} lists all laws for selective functors, as well as some
useful theorems that can be derived from these laws and
parametricity~\citep{wadler1989theorems}. Below we discuss them in order.

The \emph{identity} and \emph{distributivity} laws determine the interaction of
the select operator with pure computations. It should be impossible to
distinguish \hs{x}~\hs{<*?}~\hs{pure}~\hs{id} from a direct execution of \hs{x}
followed by the extraction of a value from the obtained
\hs{Either}~\hs{a}~\hs{a}. In particular, the select operator is not allowed to
duplicate effects associated with \hs{x}. Similarly, the select operator is not
allowed to sneak in any effects if the first computation is pure, which allows
\hs{pure}~\hs{x}~\hs{<*?} to be distributed through the applicative sequencing
operator \hs{*>}. When applied in reverse, the distributivity law allows us to
simplify sequences of conditional operations \emph{as long as the conditions are
pure}. The \emph{generalised identity} theorem follows from the identity law by
parametricity.

\begin{figure}
\begin{minted}{haskell}
-- Identity
@\blk{x}@ <*? pure id = either id id <$> x
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- Distributivity; note that y and z have the same type f (a -> b)
@\blk{pure}@ x <*? (y *> z) = (pure x <*? y) *> (pure x <*? z)
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- Associativity
@\blk{x}@ <*? (y <*? z) = (f <$> x) <*? (g <$> y) <*? (h <$> z)
  where
    f x = Right <$> x
    g y = \a -> bimap (,a) ($a) y
    h z = uncurry z
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- For selective functors that are also monads:
@\blk{select}@ = selectM
\end{minted}
\vspace{2mm}
\hrule
\vspace{2mm}
\begin{minted}{haskell}
-- Apply a pure function to the result
@\blk{f}@ <$> select x y = select (fmap f <$> x) (fmap f <$> y)
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- Apply a pure function to the Left case of the first argument
@\blk{select}@ (first f <$> x) y = select x ((. f) <$> y)
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- Apply a pure function to the second argument
@\blk{select}@ x (f <$> y) = select (first (flip f) <$> x) ((&) <$> y)
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- Generalised identity
@\blk{x}@ <*? pure y = either y id <$> x
\end{minted}
\vspace{1mm}
\begin{minted}{haskell}
-- Laws for rigid selective functors (in particular, for monads)
(<*>) = apS                     -- Selective apply
@\blk{x}@ *> (y <*? z) = (x *> y) <*? z -- Interchange
\end{minted}
\vspace{-2mm}
\caption{Laws (top) and theorems (bottom) of selective functors. Coq proofs that
the selective instances from~\S\ref{sec-instances} are lawful are available in
the supplementary material.}
\label{fig-laws}
\vspace{-3mm}
\end{figure}
% -- General interchange
% x <*> (y <*? z) = (f <$> x <*> y) <*? (g <$> z)
%   where
%     f ab = bimap (\c ca -> ab (ca c)) ab
%     g ca = ($ca)
% -- General distributivity (?)
% ifS (pure x) a1 b1 *> ifS (pure x) a2 b2 = ifS (pure x) (a1 *> a2) (b1 *> b2)

Note that it is not a requirement for selective functors to skip unnecessary
effects. In particular, we do not require that
\hs{pure}~\hs{(Right}~\hs{x)}~\hs{<*?}~\hs{y}~\hs{=}~\hs{pure}~\hs{x}. It may be
counterintuitive, but omitting this law makes selective functors more useful.
Typically, \emph{when executing} a selective computation, you would want to skip
the unnecessary effects (saving work); but on the other hand, if your goal is to
\emph{statically analyse} a given selective computation and extract the set of
all possible effects (without actually executing them), then you do not want to
skip any effects, because this would defeat the purpose of static analysis.
\hs{Over} is an example of a selective functor that would violate the
requirement to skip unnecessary effects. Similarly, we do not require that
\hs{pure}~\hs{(Left}~\hs{x)}~\hs{<*?}~\hs{y}~\hs{=}~\hs{(@\dollar@}\hs{x)}~\hs{<@\dollar@>}~\hs{y}
thus legalising under-approximating instances like \hs{Under}. It is worth
noting, however, that the monadic select operator \hs{selectM} does satisfy the
\emph{pure Left} and \emph{pure Right} properties.

Such loose requirements on \hs{select} with respect to unnecessary effects might
seem troublesome. Below we list three reasons why we chose to keep the
requirements loose.
\begin{itemize}
    \item Requiring unnecessary effects to be skipped rules out any instances
    that could be used for static analysis. Indeed, the only way to obey this
    law would be to look at actual values at runtime, since it is impossible to
    statically know if \hs{f}~\hs{(Either}~\hs{a}~\hs{b)} contains a \hs{Right}
    value.
    \item One might suggest having two classes \hs{Selective} and
    \hs{StrictSelective}, the latter with stricter requirements. But this second
    class would be useless: to statically analyse a computation you would have
    to express it via \hs{Selective}, since it is inhabited by instances like
    \hs{Over}. As for the execution, a \hs{Monad} is perfectly suitable, as we
    will see in~\S\ref{sec-free-ping-pong}.
    \item Finally, there is a good precedent: the \hs{Applicative} type class
    has no requirements on the order in which effects are executed:
    left-to-right, right-to-left, or in parallel. This loose specification
    allows some instances to execute effects sequentially (typically from left
    to right), and other instances to execute effects in parallel. Note that as
    soon as we also have a \hs{Monad}, we gain an additional requirement
    \hs{(<*>)}~\hs{=}~\hs{ap}, which tells us that, at the semantic level, the
    result should be as if the effects were executed in sequence from left to
    right. We follow the same approach by requiring
    \hs{select}~\hs{=}~\hs{selectM} if \hs{f} is also a \hs{Monad} (see below).
\end{itemize}

% General limitation of reasoning using laws:
% \item One might argue that stricter requirements would make it easier to
% reason about functions with the \hs{Selective}~\hs{f} constraint. This is
% not the case, since effect skipping/duplication is
% is not typically tracked in types at all
% as evidenced by a simple example:
% \hs{Selective}~\hs{f}~\hs{=>}~\hs{f}~\hs{()}~\hs{->}~\hs{f}~\hs{()}.
% Regardless of the laws we choose, we cannot distinguish implementations
% \hs{id}, \hs{\x}~\hs{->}~\hs{pure}~\hs{()} and
% \hs{\x}~\hs{->}~\hs{x}~\hs{*>}~\hs{x}. Same for \hs{Applicative}~\hs{f} or
% \hs{Monad}~\hs{f}.

The \emph{associativity} law states that it should always be possible to
re-associate a sequence of select operators to the left, by doing the necessary
adjustments to the shapes of the inner values. These adjustments, called \hs{f},
\hs{g}, and \hs{h} in Fig.~\ref{fig-laws}, are admittedly obscure and require an
explanation. For the expression \hs{x}~\hs{<*?}~\hs{(}\hs{y}~\hs{<*?}~\hs{z)} to
typecheck, the arguments should have the following types:

\begin{minted}[xleftmargin=5pt,fontsize=\small]{haskell}
x :: f (Either a b)                  @\blu{y}@ :: f (Either c (a -> b))     @\blu{z}@ :: f (c -> a -> b)
\end{minted}

\noindent
On the other hand, the resulting expression
\hs{p}~\hs{<*?}~\hs{q}~\hs{<*?}~\hs{r} has arguments of these types:

\begin{minted}[xleftmargin=5pt,fontsize=\small]{haskell}
p :: f (Either a (Either (c,a) b))   @\blu{q}@ :: f (a -> Either (c,a) b)   @\blu{r}@ :: f ((c,a) -> b)
\end{minted}

\noindent
To adjust \hs{x}, we inject it in a larger sum type; \hs{y} is turned into a
function accepting a value of type \hs{a} from \hs{p}; and \hs{z} is simply
\hs{uncurry}ed. As we will see in~\S\ref{sec-alt-symmetric}, the associativity
law can be expressed much more naturally if we switch to a more symmetric select
operator. A similar phenomenon occurs with the \emph{composition} law of
the \hs{Applicative} type class when the latter is expressed using an equivalent
but more symmetric \hs{Monoidal} interface~\citep{mcbride2008applicative}.

The final law links selective functors to monads: in the spirit of the
conventional applicative-monad law \hs{(<*>)}~\hs{=}~\hs{ap}, we require that
monadic instances implement \hs{select} so that it is extensionally equivalent
to \hs{selectM}, which in particular means that unnecessary effects are skipped.
A consequence of this law is that monadic selective functors are also rigid,
i.e. \hs{(<*>)}~\hs{=}~\hs{apS}, which makes it practically feasible to reason
about code written using all three abstractions.

Fig.~\ref{fig-laws} also lists a few theorems that are useful when working with
selective functors. The first three come for free from parametricity: they tell
how one can reshape pure contents of selective functors. Last but not least, the
\emph{interchange} property is a consequence of associativity and
\hs{(<*>)}~\hs{=}~\hs{apS}, which allows us to move computations inside the
condition argument of the select operator. This property does not always hold
for non-rigid selective functors: while \hs{Under} respects it, \hs{Validation}
does not, as demonstrated by the following example:

\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ whenS (Under "a" *> Under "b") (Under "c")
Under "ab"
@\ghci@ Under "a" *> whenS (Under "b") (Under "c")
Under "ab"
\end{minted}
\vspace{0mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ Failure "a" *> whenS (Success True) (Failure "b")
Failure "ab"
@\ghci@ whenS (Failure "a" *> Success True) (Failure "b")
Failure "a"
\end{minted}
\vspace{0.5mm}

\noindent
One example where the interchange law appears naturally is parser combinators,
where it allows us to refactor parsers with choice,
see~\S\ref{sec-alternative-functors}.
